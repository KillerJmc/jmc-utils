plugins {
    id "java"
    id "maven-publish"
    id "io.freefair.aspectj.post-compile-weaving" version "6.6.2"
}

projectInfo {
    group = "com.jmc"
    version = "3.8.0"
}

repositories {
    mavenCentral()
}

dependencies {
    ext {
        aspectjVersion = "1.9.21.1"
        lombokVersion = "1.18.30"
        junitVersion = "4.13.2"
    }

    implementation "org.aspectj:aspectjweaver:$aspectjVersion"
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation "junit:junit:$junitVersion"
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    destinationDir = file("docs")
    options {
        encoding = "UTF-8"
        tags = ["apiNote:a:API Note:"]
    }
}

publishing {
    publications {
        create("release", MavenPublication) {
            from((SoftwareComponent) components["java"])
        }
    }

    repositories {
        maven {
            url = "$rootDir/repo"
        }
    }
}

def projectInfo(Closure closure) {
    closure.delegate = project
    closure()
}
