plugins {
    id "java"
    id "maven-publish"
    id "io.freefair.aspectj.post-compile-weaving" version "6.6.2"
}

group "com.jmc"
version "3.2.0"
sourceCompatibility = "16"

repositories {
    mavenCentral()
}

ext {
    aspectjVersion = "1.9.19"
    lombokVersion = "1.18.24"
    junitVersion = "4.13.2"
}

dependencies {
    implementation "org.aspectj:aspectjrt:$aspectjVersion"
    runtimeOnly "org.aspectj:aspectjweaver:$aspectjVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation "junit:junit:$junitVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
}

tasks.named("test") {
    // 添加AspectJ LTW的VM参数
    jvmArgs += ["--add-opens", "java.base/java.lang=ALL-UNNAMED"]
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.encoding = "UTF-8"
    destinationDir = file("docs")
    // 添加javadoc注解 @apiNote
    options.tags = ["apiNote:a:API Note:"]
}

publishing {
    publications {
        release(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            url = "$rootDir/repo"
        }
    }
}
